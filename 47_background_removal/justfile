set dotenv-load := true

# default lists actions
default:
  @just -f justfile --list

clean:
  rm -rf ./.venv || true
  rm Pipfile.lock || true

install:
  #!/usr/bin/env bash
  export PIPENV_IGNORE_VIRTUALENVS=1
  export PIPENV_VENV_IN_PROJECT=1
  pipenv install --dev --python=$(pyenv which python) --verbose

start:
  pipenv run start:test


build_image distro:
  docker buildx build --progress=plain --load -f Dockerfile.{{ distro }} -t 47_background_removal{{ distro }}:latest .

start_image distro: (build_image distro)
  docker run 47_background_removal{{ distro }}:latest

dive distro: (build_image distro)
  dive 47_background_removal{{ distro }}:latest

details distro: (build_image distro)
  @echo "******************************"
  @echo "** Labels"
  @echo "******************************"
  docker inspect -f '{{{{ .Config.Labels }}}}' 47_background_removal{{ distro }}:latest
  @echo "******************************"
  @echo "** Dive CI"
  @echo "******************************"
  dive 47_background_removal{{ distro }}:latest --ci || true
  @echo "******************************"
  @echo "** Size"
  @echo "******************************"
  container-diff analyze --json daemon://47_background_removal{{ distro }}:latest | jq .

