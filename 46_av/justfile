set dotenv-load := true

# default lists actions
default:
  @just -f justfile --list

clean:
  rm -rf ./.venv || true
  rm Pipfile.lock || true

install:
  #!/usr/bin/env bash
  export PIPENV_IGNORE_VIRTUALENVS=1
  export PIPENV_VENV_IN_PROJECT=1
  pipenv install --dev --python=$(pyenv which python) --verbose

start operation:
  pipenv run python main.py --file ./out/testcard/testcard_960_720p_30fps.mp4 --operation {{ operation }}

start_audio operation:
  pipenv run python main.py --file ./out/audio/LNL305.mp3 --operation {{ operation }}

build_image distro:
  docker buildx build --progress=plain --load -f Dockerfile.{{ distro }} -t 46_av{{ distro }}:latest .

start_image distro: (build_image distro)
  docker run 46_av{{ distro }}:latest

dive distro: (build_image distro)
  dive 46_av{{ distro }}:latest

details distro: (build_image distro)
  @echo "******************************"
  @echo "** Labels"
  @echo "******************************"
  docker inspect -f '{{{{ .Config.Labels }}}}' 46_av{{ distro }}:latest
  @echo "******************************"
  @echo "** Dive CI"
  @echo "******************************"
  dive 46_av{{ distro }}:latest --ci || true
  @echo "******************************"
  @echo "** Size"
  @echo "******************************"
  container-diff analyze --json daemon://46_av{{ distro }}:latest | jq .

