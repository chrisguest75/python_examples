set dotenv-load := true

export DEFAULT_APPLICATION:="uv"

# default lists actions
default:
  @just -f justfile --list

nix:
  #!/usr/bin/env bash
  set -xeufo pipefail
  nix develop --command zsh

init:
  #!/usr/bin/env bash
  set -xeufo pipefail
  uv init .

start-server *args:
  #!/usr/bin/env bash
  set -eufo pipefail
  rm -f ./recording/recorded_audio.wav 
  uv run ./main.py {{ args }} 

start-client *args:
  #!/usr/bin/env bash
  set -eufo pipefail
  uv run ./main.py {{ args }} 

test: 
  #!/usr/bin/env bash
  set -eufo pipefail
  uv run pytest

lint:  
  #!/usr/bin/env bash
  set -eufo pipefail
  uv run flake8

format:
  #!/usr/bin/env bash
  uv run black --line-length=120 .

# build docker image 
docker-build application="$DEFAULT_APPLICATION": 
  #!/usr/bin/env bash
  set -xeufo pipefail  
  docker build --progress=plain -f Dockerfile.{{ application }} -t 51_websockets_{{ application }}:latest .

# get shell in docker container
docker-shell application="$DEFAULT_APPLICATION": (docker-build application)
  #!/usr/bin/env bash
  set -xeufo pipefail  
  docker run -it --entrypoint bash 51_websockets_{{ application }}:latest

# run docker image
docker-run application="$DEFAULT_APPLICATION": (docker-build application)
  #!/usr/bin/env bash
  set -xeufo pipefail  
  docker run -it -p 5900:5900 51_websockets_{{ application }}:latest

docker-dive application: (docker-build application)
  #!/usr/bin/env bash
  set -xeufo pipefail  
  dive 51_websockets_{{ application }}:latest

docker-details application: (docker-build application)
  @echo "******************************"
  @echo "** Labels"
  @echo "******************************"
  docker inspect -f "{{{{ .Config.Labels }}}}" 51_websockets_{{ application }}:latest
  @echo "******************************"
  @echo "** Dive CI"
  @echo "******************************"
  dive 51_websockets_{{ application }}:latest --ci || true
  @echo "******************************"
  @echo "** Size"
  @echo "******************************"
  container-diff analyze --json daemon://51_websockets_{{ application }}:latest | jq .

